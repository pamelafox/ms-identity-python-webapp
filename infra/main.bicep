targetScope = 'subscription'

@minLength(1)
@maxLength(64)
@description('Name which is used to generate a short unique hash for each resource')
param name string

@minLength(1)
@description('Primary location for all resources')
param location string

var resourceToken = toLower(uniqueString(subscription().id, name, location))
var tags = { 'azd-env-name': name }

resource resourceGroup 'Microsoft.Resources/resourceGroups@2021-04-01' = {
  name: '${name}-rg'
  location: location
  tags: tags
}

var prefix = '${name}-${resourceToken}'

module web 'core/host/appservice.bicep' = {
  name: 'appservice'
  scope: resourceGroup
  params: {
    name: '${prefix}-appservice'
    location: location
    tags: union(tags, { 'azd-service-name': 'web' })
    appServicePlanId: appServicePlan.outputs.id
    runtimeName: 'python'
    runtimeVersion: '3.10'
    scmDoBuildDuringDeployment: true
    ftpsState: 'Disabled'
    appSettings: {
        REDIS_HOST: redisCache.outputs.hostName
        REDIS_PORT: redisCache.outputs.sslPort
        REDIS_PASSWORD: redisCache.outputs.password
    }
  }
}

module redisCache 'redis.bicep' = {
  name: 'redis'
  scope: resourceGroup
  params: {
    name: '${prefix}-redis'
    location: location
    tags: tags
    workspaceId: logAnalyticsWorkspace.outputs.id
  }
}

module appServicePlan 'core/host/appserviceplan.bicep' = {
  name: 'serviceplan'
  scope: resourceGroup
  params: {
    name: '${prefix}-serviceplan'
    location: location
    tags: tags
    sku: {
      name: 'B1'
    }
    reserved: true
  }
}


module logAnalyticsWorkspace 'core/monitor/loganalytics.bicep' = {
  name: 'loganalytics'
  scope: resourceGroup
  params: {
    name: '${prefix}-loganalytics'
    location: location
    tags: tags
  }
}

output WEB_URI string = 'https://${web.outputs.uri}'
output WEB_NAME string = web.outputs.name
output AZURE_LOCATION string = location
output RESOURCE_GROUP_NAME string = resourceGroup.name
